> Материал на этой странице относится к Elm 0.18

# Структура Html.program

### Импорты

```elm
import Html exposing (Html, div, text, program)
```

- Мы будем использовать тип `Html` из модужя `Html`, а также несколько функций: `div`, `text` и `program`.

### Модель (model)

```elm
type alias Model =
    String


init : ( Model, Cmd Msg )
init =
    ( "Hello", Cmd.none )
```

- Для начала определим модель нашего приложения как синоним типа. Сейчас это просто `String`.
- Теперь определим функцию `init`. Она задаст начальное состояние приложения.

__Html.program__ получает на вход кортеж `(model, command)`. Первый элемент в кортеже - начальное состояние приложения, в нашем случае "Hello". Второй - начальная команда на выполнение. В дальнейшем мы разберём этот момент подробно.

При использовании Архитектуры Elm, мы объединяем все модели компонентов в единое дерево состояния. Об этом также чуть позже.

### Сообщения (messages)

```elm
type Msg
    = NoOp
```

Сообщения - это сущности, которые появляются в приложении, и на которые реагируют компоненты. В нашем случае, приложение ничего не делает, поэтому сообщение определяем всего одно -  `NoOp` (пустая операция).

Другими сообщениями могли бы быть, например, `Expand` или `Collapse`, показывающие или скррывающте виджет. Для сообщений используются составные типы:

```elm
type Msg
    = Expand
    | Collapse
```

### Представление (view)

```elm
view : Model -> Html Msg
view model =
    div []
        [ text model ]
```

Функция `view` отрисовывает элемент Html, используя модель нашего приложения. Обратите внимание на её определение: `Html Msg`, оно говорит о том, что этот элемент Html будет производить сообщения типа Msg. Мы пронаблюдаем это, когда добавим немного интерактивности.

### Обновление (update)

```elm
update : Msg -> Model -> ( Model, Cmd Msg )
update msg model =
    case msg of
        NoOp ->
            ( model, Cmd.none )
```

Затем мы определяем функцию `update`, которая будет вызываться `Html.program` при каждом получении сообщения. Реакцией функции обновления на сообщения является необходимое обновление модели и возвращение необходимых команд.

В этом примере мы обрабатываем `NoOp` и возвращаем неизменённую модель икоманду `Cmd.none` (то есть, не даём никаких команд).

### Подписки (subscriptions)

```elm
subscriptions : Model -> Sub Msg
subscriptions model =
    Sub.none
```

Мы используем подписки для получения внешнего ввода в приложения. Вот примеры подписок:

- перемещение мыши;
- события клавиатуры;
- изменения в адресе страницы.

В этом случае мы не заинтересованы в обработке какого-либо внешнего ввода, поэтому используем `Sub.none`.

### Главная функция (main)

```elm
main : Program Never Model Msg
main =
    program
        { init = init
        , view = view
        , update = update
        , subscriptions = subscriptions
        }
```

Наконец, `Html.program` связывает всё вместе и возвращает элемент html, который мы можем отрисовать на странице. `program` принимает наши `init`, `view`, `update` и `subscriptions`.
